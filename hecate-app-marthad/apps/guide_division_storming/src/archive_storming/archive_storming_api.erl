%%% @doc API handler: POST /api/stormings/:division_id/archive
-module(archive_storming_api).

-include("storming_status.hrl").

-export([init/2, routes/0]).

routes() -> [{"/api/stormings/:division_id/archive", ?MODULE, []}].

init(Req0, State) ->
    case cowboy_req:method(Req0) of
        <<"POST">> -> handle_post(Req0, State);
        _ -> app_marthad_api_utils:method_not_allowed(Req0)
    end.

handle_post(Req0, _State) ->
    DivisionId = cowboy_req:binding(division_id, Req0),
    case DivisionId of
        undefined ->
            app_marthad_api_utils:bad_request(<<"division_id is required">>, Req0);
        _ ->
            CmdParams = #{division_id => DivisionId},
            case archive_storming_v1:new(CmdParams) of
                {ok, Cmd} -> dispatch(Cmd, Req0);
                {error, Reason} -> app_marthad_api_utils:bad_request(Reason, Req0)
            end
    end.

dispatch(Cmd, Req) ->
    case maybe_archive_storming:dispatch(Cmd) of
        {ok, _Version, EventMaps} ->
            DivisionId = archive_storming_v1:get_division_id(Cmd),
            Status = evoq_bit_flags:set(0, ?STORMING_ARCHIVED),
            StatusLabel = evoq_bit_flags:to_string(Status, ?STORMING_FLAG_MAP),
            app_marthad_api_utils:json_ok(200, #{
                division_id => DivisionId,
                status => Status,
                status_label => StatusLabel,
                events => EventMaps
            }, Req);
        {error, Reason} ->
            app_marthad_api_utils:bad_request(Reason, Req)
    end.
